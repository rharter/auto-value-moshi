buildscript {
  repositories {
    mavenCentral()
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }

  dependencies {
    classpath 'net.ltgt.gradle:gradle-apt-plugin:0.19'
  }
}

allprojects { project ->
  project.repositories {
    mavenCentral()
  }

  project.apply from: rootProject.file('dependencies.gradle')

  project.plugins.withId("net.ltgt.apt") {
    try {
      // automatically apply net.ltgt.apt-idea whenever net.ltgt.apt is used
      project.apply plugin: "net.ltgt.apt-idea"
      // disable addAptDependencies (if you delegate build actions to Gradle)
      project.plugins.withType(JavaPlugin) {
        project.afterEvaluate {
          project.idea.module.apt.addAptDependencies = false
        }
      }
    } catch (ignored) {
      // ignore, in case an older version of net.ltgt.apt is being used
      // that doesn't come with net.ltgt.apt-idea.
    }
  }
}

subprojects { project ->
  apply plugin: 'checkstyle'

  checkstyle {
    toolVersion '7.2'
    configFile rootProject.file('checkstyle.xml')
  }

  afterEvaluate {
    tasks.withType(Checkstyle) {
      configProperties = [
              'proj.module.dir'      : projectDir.absolutePath,
              'checkstyle.cache.file': './build/cache/checkstyle-cache'
      ]
    }
  }
}
